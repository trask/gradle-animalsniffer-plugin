pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        google()
        // for github package (used on CI)
        maven {
            url  = 'https://maven.pkg.github.com/xvik/gradle-animalsniffer-plugin'
            credentials {
                username = settings.ext.find('gpr.user') ?: System.getenv("USERNAME")
                password = settings.ext.find('gpr.key') ?: System.getenv("TOKEN")
            }
        }
    }
}
// avoid caching plugin snapshot
buildscript {
    configurations.classpath {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

enableFeaturePreview "STABLE_CONFIGURATION_CACHE"

include 'java', 'groovy', 'kotlin', 'scala',
        'android-lib', 'android-app',
        'buildSignature:fromClasses',
        'buildSignature:fromJars',
        'buildSignature:fromSignatures',
        'buildSignature:fromMix'

if (JavaVersion.current() >= JavaVersion.VERSION_17) {
    include 'android-kotlin-multiplatform', 'kotlin-multiplatform'
}